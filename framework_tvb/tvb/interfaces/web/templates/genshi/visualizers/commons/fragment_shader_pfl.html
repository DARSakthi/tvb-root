<script id="shader-fs" type="x-shader/x-fragment">
    precision mediump float;

    uniform vec3 uAmbientColor;
    uniform vec3 uLightingDirection;
    uniform vec3 uDirectionalColor;
    uniform vec3 uLinesColor;

    uniform float uMaterialShininess;
    uniform vec3 uPointLightingLocation;
    uniform vec3 uPointLightingSpecularColor;

    // these are used to draw whole objects in a specific color ignoring vertex attributes.
    // for vertex level picking special color buffers are used.
    uniform bool uUseVertexColors;
    uniform vec4 uMaterialColor;

    varying vec4 vColor;
    varying vec3 posInterp;
    varying vec3 normInterp;

    void main(void) {
        vec3 normal = normalize(normInterp);
        // ignore uPointLightingLocation. Make the camera (0,0,0) the point light source
        vec3 lightDirection = normalize(vec3(0.0, 0.0, 0.0) - posInterp);
        vec3 reflectionDirection = reflect(-lightDirection, normal);
        vec3 eyeDirection = normalize(-posInterp);

        float directionalLightWeighting = max(dot(normal, uLightingDirection), 0.0);
        float specularLightWeighting = pow(max(dot(reflectionDirection, eyeDirection), 0.0), uMaterialShininess);

        vec4 materialColor;

        if (uUseVertexColors){
            materialColor = vColor;
        } else {
            materialColor = uMaterialColor;
        }

        vec3 diffuse = uAmbientColor + uDirectionalColor * directionalLightWeighting;
        vec3 light = materialColor.rgb * diffuse + uPointLightingSpecularColor * specularLightWeighting;
        gl_FragColor = vec4(light, materialColor.a);
    }
</script>